plugins {
    id 'java'
    id 'application'
    id 'checkstyle'
    id 'com.github.johnrengelman.shadow' version '5.1.0'
    id 'org.asciidoctor.convert' version '1.5.8'
}

group 'seedu.OwlMoney'
version '1.2.0'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.5.1'
    testRuntime('org.junit.jupiter:junit-jupiter-engine:5.5.1')
}

checkstyle {
    toolVersion = '8.23'
}

shadowJar {
    archiveBaseName = "OwlMoney"
    archiveVersion = "v1.2"
    archiveClassifier = null
    archiveAppendix = null
}

application {
    // Change this to your main class.
    mainClassName = "owlmoney.Main"
}

run {
    standardInput = System.in
}

test {
    useJUnitPlatform()
}

asciidoctor {
    backends 'html5'
    sourceDir 'docs'
    outputDir "${buildDir}/docs"

    options = [
            template_dirs: [file("${sourceDir}/templates")],
    ]

    attributes = [
            linkcss: true,
            stylesheet: 'gh-pages.css',
            'source-highlighter': 'coderay',
            icons: 'font',
            experimental: true,
            sectlinks: true,
            idprefix: '',  // for compatibility with GitHub preview
            idseparator: '-',
            'site-root': "${sourceDir}",  // must be the same as sourceDir, do not modify
            'site-name': 'AddressBook-Level4',
            'site-githuburl': 'https://github.com/se-edu/addressbook-level4',
            'site-seedu': true,  // delete this line if your project is not a fork (not a SE-EDU project)
    ]

    options['template_dirs'].each {
        inputs.files fileTree(it)
    }
}

// Copies stylesheets into the directory containing generated HTML files as
// Asciidoctor does not copy linked CSS files to the output directory when rendering.
// This is needed for linked stylesheets and embedded stylesheets which import other files.
task copyStylesheets(type: Copy) {
    from "${asciidoctor.sourceDir}/stylesheets"
    into "${asciidoctor.outputDir}/html5/stylesheets"
}
asciidoctor.dependsOn copyStylesheets

task deployOfflineDocs(type: Copy) {
    into('src/main/resources/docs')

    from ("${asciidoctor.outputDir}/html5") {
        include 'stylesheets/*'
        include 'images/*'
        include 'HelpWindow.html'
    }
}

deployOfflineDocs.dependsOn asciidoctor
processResources.dependsOn deployOfflineDocs